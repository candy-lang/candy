bool = use "..bool"
[check] = use "..check"
[ifElse, recursive] = use "..controlFlow"
[equals] = use "..equality"
[run] = use "..function"
int = use "..int"
struct = use "..struct"
tag = use "..tag"
text = use "..text"
[toDebugText] = use "..toDebugText"

isScale scale = int.is scale | bool.lazyAnd { int.isNonNegative scale }
scaleFactor scale =
  needs (isScale scale)
  10 | int.pow scale

is a := a %
  FixedDecimal [minorUnits, scale] -> int.is minorUnits | bool.lazyAnd { isScale scale }
  _ -> False

minorUnits a :=
  needs (is a)
  (a | tag.getValue).minorUnits
scale a :=
  needs (is a)
  (a | tag.getValue).scale

fromIntScaled minorUnits scale :=
  needs (int.is minorUnits)
  needs (isScale scale)
  FixedDecimal [minorUnits, scale]
fromInt a :=
  needs (int.is a)
  fromIntScaled a 0
floorToInt a :=
  needs (is a)
  a | minorUnits | int.divideTruncating (a | scale | scaleFactor)

zero := fromInt 0
one := fromInt 1

rescaledMinorUnits a targetScale =
  needs (is a)
  needs (isScale targetScale)
  ifElse
    a | scale | int.isGreaterThan targetScale
    { a | minorUnits | int.divideTruncating (scaleFactor (a | scale | int.subtract targetScale)) }
    { a | minorUnits | int.multiply (scaleFactor (targetScale | int.subtract (a | scale))) }
rescale a targetScale :=
  needs (is a)
  needs (isScale targetScale)
  fromIntScaled (a | rescaledMinorUnits targetScale) targetScale
comparableMinorUnits valueA valueB =
  needs (is valueA)
  needs (is valueB)
  targetScale = int.max (valueA | scale) (valueB | scale)
  [
    ValueA: rescaledMinorUnits valueA targetScale,
    ValueB: rescaledMinorUnits valueB targetScale,
    targetScale,
  ]

add summandA summandB :=
  needs (is summandA)
  needs (is summandB)
  [valueA, valueB, targetScale] = comparableMinorUnits summandA summandB
  fromIntScaled (valueA | int.add valueB) targetScale
subtract minuend subtrahend :=
  needs (is minuend)
  needs (is subtrahend)
  [valueA, valueB, targetScale] = comparableMinorUnits minuend subtrahend
  fromIntScaled (valueA | int.subtract valueB) targetScale
negate value :=
  needs (is value)
  fromIntScaled (value | minorUnits | int.negate) (value | scale)
multiply factorA factorB :=
  needs (is factorA)
  needs (is factorB)
  fromIntScaled
    factorA | minorUnits | int.multiply (factorB | minorUnits)
    factorA | scale | int.add (factorB | scale)
square value :=
  needs (is value)
  value | multiply value
divideTruncating dividend divisor :=
  needs (is dividend)
  needs (is divisor)
  needs (divisor | minorUnits | equals 0 | bool.not) "You can't divide by zero."
  [valueA, valueB, targetScale] = comparableMinorUnits dividend divisor
  fromIntScaled
    valueA | int.multiply (targetScale | scaleFactor) | int.divideTruncating valueB
    targetScale
divideTruncatingAtScale dividend divisor targetScale :=
  needs (is dividend)
  needs (is divisor)
  needs (isScale targetScale)
  needs (divisor | minorUnits | equals 0 | bool.not) "You can't divide by zero."
  [valueA, valueB] = comparableMinorUnits dividend divisor
  fromIntScaled
    valueA | int.multiply (targetScale | scaleFactor) | int.divideTruncating valueB
    targetScale

compareTo valueA valueB :=
  needs (is valueA)
  needs (is valueB)
  [valueA, valueB] = comparableMinorUnits valueA valueB
  result = valueA | int.compareTo valueB
  check (equals result Equal | bool.implies (equals valueA valueB))
  result
isLessThan valueA valueB :=
  needs (is valueA)
  needs (is valueB)
  equals (compareTo valueA valueB) Less
isGreaterThan valueA valueB :=
  needs (is valueA)
  needs (is valueB)
  equals (compareTo valueA valueB) Greater
isLessThanOrEqualTo valueA valueB :=
  needs (is valueA)
  needs (is valueB)
  valueA | isGreaterThan valueB | bool.not
isGreaterThanOrEqualTo valueA valueB :=
  needs (is valueA)
  needs (is valueB)
  valueA | isLessThan valueB | bool.not

isPositive value :=
  needs (is value)
  value | minorUnits | int.isGreaterThan 0
isNonPositive value :=
  needs (is value)
  value | isPositive | bool.not
isNegative value :=
  needs (is value)
  value | minorUnits | int.isLessThan 0
isNonNegative value :=
  needs (is value)
  value | isNegative | bool.not
absolute value :=
  needs (is value)
  ifElse (isNegative value) { negate value } { value }

approxEquals a b delta :=
  needs (is a)
  needs (is b)
  needs (is delta)
  needs (isNonNegative delta)
  a | subtract b | absolute | isLessThanOrEqualTo delta

min valueA valueB :=
  needs (is valueA)
  needs (is valueB)
  ifElse (valueA | isLessThanOrEqualTo valueB) { valueA } { valueB }
max valueA valueB :=
  needs (is valueA)
  needs (is valueB)
  ifElse (valueA | isGreaterThanOrEqualTo valueB) { valueA } { valueB }
coerceAtLeast value minimum :=
  needs (is value)
  needs (is minimum)
  max value minimum
coerceAtMost value maximum :=
  needs (is value)
  needs (is maximum)
  min value maximum
coerceIn value minimum maximum :=
  needs (is value)
  needs (is minimum)
  needs (is maximum)
  needs (minimum | isLessThanOrEqualTo maximum)
  value | coerceAtLeast minimum | coerceAtMost maximum

toText value :=
  needs (is value)
  beforeDot = value | floorToInt
  afterDot = run {
    scaleFactor = value | scale | scaleFactor
    tmp = value | minorUnits | int.remainder scaleFactor
    ifElse
      int.isNonNegative tmp
      { tmp | int.add scaleFactor | toDebugText | text.removePrefix "1" }
      { tmp | int.subtract scaleFactor | toDebugText | text.removePrefix "-1" }
  }
  "{beforeDot}.{afterDot}"
