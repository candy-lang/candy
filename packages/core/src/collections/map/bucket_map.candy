use ....bool
use ....hash
use ....maybe
use ....numbers
use ....operators
use ....todo
use ...array
use ...iterable
use ...list
use ...map
use ..linked_hash_map

// TODO(never, marcelgarus): Make the `BucketMap` implementation way more efficient.
// TODO(never, marcelgarus): Typedef Bucket as LinkedHashMap.

class BucketMap<Key: Equals & Hash, Value> {
  static fun empty<Key: Equals & Hash, Value>(): BucketMap<Key, Value> {
    BucketMap<Key, Value>(
      Array.generate<LinkedHashMap<Key, Value>>(8, { LinkedHashMap.empty<Key, Value>() }),
      0,
    )
  }

  let buckets: Array<LinkedHashMap<Key, Value>>
  mut let numEntries: Int

  fun bucketByHash(hash: Int): LinkedHashMap<Key, Value> { buckets.get(hash % buckets.length()) }
  fun bucketByKey(key: Key): LinkedHashMap<Key, Value> { bucketByHash(key.hashCode_()) }
}
impl<Key: Equals & Hash, Value> BucketMap<Key, Value>: Equals & Hash & Map<Key, Value> {
  fun length(): Int { numEntries }

  fun get(key: Key): Maybe<Value> { bucketByKey(key).get(key) }

  fun entries(): Iterable<(Key, Value)> {
    let list = MutableList.empty<(Key, Value)>()
    for bucket in buckets.toList() {
      for entry in bucket.entries() {
        list.append(entry)
      }
    }
    list as Iterable<(Key, Value)>
  }
}
impl<Key: Equals & Hash, Value> BucketMap<Key, Value>: MutableMap<Key, Value> {
  fun set(key: Key, value: Value) {
    let bucket = bucketByKey(key)
    if !bucket.containsKey(key) {
      numEntries = numEntries + 1
    }
    bucket.set(key, value)
  }

  fun putIfAbsent(key: Key, ifAbsent: () => Value): Value {
    let bucket = bucketByKey(key)
    if !bucket.containsKey(key) {
      bucket.set(key, ifAbsent())
      numEntries = numEntries + 1
    }
    bucket.get(key).unwrap()
  }

  fun remove(key: Key): Maybe<Value> {
    let bucket = bucketByKey(key)
    if bucket.containsKey(key) {
      let result = bucket.remove(key)
      numEntries = numEntries - 1
      return result
    }
    None<Value>()
  }

  // TODO(never, marcelgarus): Implement `cast` more efficiently than the default one. We can do this by
  // using the precomputed `hash`es â€“ regardless of the type, those won't change. That also means
  // the buckets stay the same.
  // fun cast<NewKey, NewValue>(): Map<NewKey, NewValue>
}
