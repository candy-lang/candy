# Repeatedly asks you for a guess and tells you whether the secret number is
# higher or lower. Exits once you found the secret number.

[equals, ifElse, int, loop, result, function, text] = use "Core"
[run] = function

main := { environment ->
  print message = environment.stdout message
  read = { run environment.stdin }

  print "Welcome to the number guessing game!"

  correct = 42
  loop {
    print "What's your guess?"
    guess = run read | text.trim | int.parse

    guess | result.mapOrElse
      { guess ->
        ifElse
          equals guess correct
          {
            print "You did it!"
            Break
          }
          {
            print (ifElse (int.isLessThan correct guess) { "Lower!" } { "Higher!" })
            Continue
          }
      }
      { error ->
        print "Your guess must be a number."
        Continue
      }
  }
}
